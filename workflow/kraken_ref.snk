import os
from scripts import utilities

kdb = utilities.get_softpath(config, 'krakendb')
kdb_fasta = os.path.join(kdb, 'library', 'library.fna.gz')
kdb_fasta = config['softparams']['soft'].get('krakendb_lib', kdb_fasta)
kdb_meta = kdb_fasta + '.meta.tsv.gz'

rule kraken_metafromlib:
	input:
		kdb_fasta
	output:
		kdb_meta
	script:
		'scripts/kraken_metafromlib.py'

def getreads(wc):
    rtype = wc.krtype
    group = wc.group

    if rtype == 'illumina':
        r1 = f'reads/{group}/illumina/merged/R1.fastq.gz'
        r2 = f'reads/{group}/illumina/merged/R2.fastq.gz'
        return [r1, r2]

    return f'reads/{group}/{rtype}/merged.fastq.gz'

rule kraken:
    input:
        reads = getreads,
        db = utilities.get_softpath(config, 'krakendb')
    output:
        report = 'kraken/{group}/kraken.{krtype}.report.tsv',
        output = 'kraken/{group}/kraken.{krtype}.output.tsv'
    threads:
        config['miscs']['GENERIC_THREADS']
    conda:
        config['softparams']['conda']['kraken2']
    params:
        extraopt = lambda wc: '--paired' if wc.krtype == 'illumina' else ''
    benchmark:
        'benchmarks/{group}/kraken/run.{krtype}.txt'
    shell:
        'kraken2 --db {input.db} --threads {threads} --output {output.output} \
            --report {output.report} --gzip-compressed {params.extraopt} {input.reads}'

rule compress_out:
    input:
        rules.kraken.output.output
    output:
        'kraken/{group}/kraken.{krtype}.output.tsv.gz'
    benchmark:
        'benchmarks/{group}/kraken/compress.{krtype}.txt'
    shell:
        'gzip {input}'

rule process_kraken:
    input:
        report = rules.kraken.output.report,
        output = rules.compress_out.output[0],
        meta = kdb_meta
    output:
        report = 'kraken/{group}/kraken.{krtype}.report.cov.tsv',
        taxlist = 'kraken/{group}/kraken.{krtype}.report.taxids.txt'
    params:
        mincov = 5
    benchmark:
        'benchmarks/{group}/kraken/process.{krtype}.txt'
    script:
        'scripts/kraken_ref_process.py'

rule make_ref:
    input:
        taxlist = rules.process_kraken.output.taxlist,
        fasta = kdb_fasta
    output:
        'kraken/{group}/ref.{krtype}.fa.gz'
    benchmark:
        'benchmarks/{group}/kraken/makeref.{krtype}.txt'
    script:
        'scripts/kraken_ref_make_ref.py'

# -----------------------------------
# For phasers with the ref mixed together

rule extract_ref:
    input:
        rules.make_ref.output[0]
    output:
        temp('kraken/{group}/ref.{krtype}.fa')
    shell:
        'zcat {input} > {output}'

rule presplit_ref_index:
    input:
        rules.extract_ref.output[0]
    output:
        temp('kraken/{group}/ref.{krtype}.fa.fai')
    conda:
        config['softparams']['conda']['minimap2']
    shell:
        'samtools faidx {input}'

# For phasers with the ref mixed together
# -----------------------------------

MINIMAP2_PRESET = {
    'nanopore': 'map-ont',
    'illumina': 'sr'
}

rule minimap2:
    input:
        reads = getreads,
        ref = rules.make_ref.output
    output:
        temp('kraken/{group}/main/{krtype}.bam')
    threads:
        config['miscs']['GENERIC_THREADS']
    params:
        preset = lambda wc: MINIMAP2_PRESET[wc.krtype]
    conda:
        config['softparams']['conda']['minimap2']
    log:
        'logs/{group}/kraken/minimap2.{krtype}.txt'
    benchmark:
        'benchmarks/{group}/kraken/minimap2.{krtype}.txt'
    shell:
        '(minimap2 -t {threads} -ax {params.preset} {input.ref} {input.reads} | \
            samtools sort --threads {threads} > {output}) 2> {log}'

rule bamindex:
    input:
        rules.minimap2.output[0]
    output:
        'kraken/{group}/main/{krtype}.bam.bai'
    conda:
        config['softparams']['conda']['minimap2']
    shell:
        'samtools index {input}'

rule samtools_depth:
    input:
        bam = rules.minimap2.output[0]
    output:
        'kraken/{group}/main/{krtype}.bam.cov.tsv.gz'
    conda:
        config['softparams']['conda']['minimap2']
    params:
        windowsize = 10000
    script: 
        'scripts/kraken_ref_samtools_depth.py'

# ----------------------------
# Split phasing part

rule baminfo:
    input:
        bam = rules.minimap2.output[0],
        bai = rules.bamindex.output[0]
    output:
        baminfo = 'kraken/{group}/main/{krtype}.bam.info.tsv.gz',
        maxrank = 'kraken/{group}/main/{krtype}.bam.info.maxrank.tsv.gz',
        top2 = 'kraken/{group}/main/{krtype}.bam.info.top2.tsv.gz'
    conda:
        config['softparams']['conda']['minimap2']
    benchmark:
        'benchmarks/{group}/kraken/baminfo.{krtype}.txt'
    script: 
        'scripts/kraken_ref_baminfo.py'

checkpoint split_fasta:
    input:
        ref = rules.make_ref.output[0]
    output:
        'kraken/{group}/split/{krtype}.split_fasta.txt'
    params:
        dname = 'kraken/{group}/split/bamfa/'
    benchmark:
        'benchmarks/{group}/kraken/split_list_contigs.{krtype}.txt'
    script:
        'scripts/kraken_ref_split_fasta.py'

rule split_get_read_names:
    input:
        rules.baminfo.output['maxrank']
    output:
        'kraken/{group}/split/bamfa/{tid}.{krtype}.fastq.ids.txt'
    shell:
        'zcat {input} | awk \'{{ if ($3 == {wildcards.tid}) {{ print }} }}\' | cut -f 1 > {output}'

# Note for the next two rules
# As weird as it seems, a grep with a lot of patterns actually
# leads to a crazy level of memory consumption, up to 20G with my tests
# cmdline: zgrep -A 3 --no-group-separator -f {input.readnames} {input.reads} | gzip > {output}

rule split_reads_single:
    input:
        readnames = rules.split_get_read_names.output[0],
        reads = getreads
    output:
        temp('kraken/{group}/split/bamfa/{tid}.{krtype}.fastq.gz')
    script:
        'scripts/kraken_split_reads.py'

rule split_reads_paired:
    input:
        readnames = 'kraken/{group}/split/bamfa/{tid}.illumina.fastq.ids.txt',
        reads = 'reads/{group}/illumina/merged/{rid}.fastq.gz'
    output:
        temp('kraken/{group}/split/bamfa/{tid}.illumina.{rid}.fastq.gz')
    script:
        'scripts/kraken_split_reads.py'

def getreadssplit(wc):
    tid = wc.tid
    krtype = wc.krtype
    group = wc.group

    if krtype == 'illumina':
        r1 = f'kraken/{group}/split/bamfa/{tid}.illumina.R1.fastq.gz'
        r2 = f'kraken/{group}/split/bamfa/{tid}.illumina.R2.fastq.gz'
        return [r1, r2]

    return f'kraken/{group}/split/bamfa/{tid}.{krtype}.fastq.gz'

rule split_minimap2:
    input:
        ref = 'kraken/{group}/split/bamfa/{tid}.{krtype}.fa.gz',
        reads = getreadssplit
    output:
        temp('kraken/{group}/split/bamfa/{tid}.{krtype}.bam')
    threads:
        config['miscs']['GENERIC_THREADS_SPLIT']
    params:
        preset = lambda wc: MINIMAP2_PRESET[wc.krtype]
    conda:
        config['softparams']['conda']['minimap2']
    log:
        'logs/{group}/kraken/split/minimap2.{tid}.{krtype}.txt'
    benchmark:
        'benchmarks/{group}/kraken/split/minimap2.{tid}.{krtype}.txt'
    shell:
        '(minimap2 -t {threads} -ax {params.preset} {input.ref} {input.reads} | \
            samtools sort --threads {threads} > {output}) 2> {log}'

rule split_sort_bam_index:
    input:
        rules.split_minimap2.output[0]
    output:
        'kraken/{group}/split/bamfa/{tid}.{krtype}.bam.bai'
    conda:
        config['softparams']['conda']['minimap2']
    shell:
        'samtools index {input}'

rule split_samtools_depth:
    input:
        bam = rules.split_minimap2.output[0]
    output:
        'kraken/{group}/split/bamfa/{tid}.{krtype}.bam.cov.tsv.gz'
    conda:
        config['softparams']['conda']['minimap2']
    params:
        windowsize = 10000
    script: 
        'scripts/kraken_ref_samtools_depth.py'

rule split_uncompress_fasta:
    input:
        'kraken/{group}/split/bamfa/{tid}.{krtype}.fa.gz'
    output:
        temp('kraken/{group}/split/bamfa/{tid}.{krtype}.fa')
    shell:
        'zcat {input} > {output}'

rule split_uncompress_fasta_index:
    input:
        rules.split_uncompress_fasta.output[0]
    output:
        'kraken/{group}/split/bamfa/{tid}.{krtype}.fa.fai'
    conda:
        config['softparams']['conda']['minimap2']
    shell:
        'samtools faidx {input}'

