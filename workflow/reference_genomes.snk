from pathlib import Path
from scripts.utilities import get_mapping_ref, get_ref

rule download_ncbinuc:
    output:
        'references/download/{group}/ncbinuc_{ncbiid}.fasta'
    conda:
        config['softparams']['conda']['entrez']
    resources:
        ncbi_load=1
    shell:
        'efetch -db nucleotide -id "{wildcards.ncbiid}" -mode text -format fasta > {output} && [[ -s {output} ]]'

rule download_ncbiasbly:
    output:
        'references/download/{group}/ncbiasbly_{ncbiid}.fasta'
    conda:
        config['softparams']['conda']['entrez']
    resources:
        ncbi_load=1
    shell:
        'esearch -db assembly -query "{wildcards.ncbiid}" | elink -target nucleotide -name assembly_nuccore_refseq | efetch -format fasta > {output} && [[ -s {output} ]]'

rule link_fasta:
    input:
        lambda wc: get_ref(wc, config)
    output:
        'references/used/{group}/sample/{sample}.fasta'
    shell:
        'ln -s $(pwd)/{input} {output}'

rule refmap:
    input:
        lambda wc: get_mapping_ref(wc, config)
    output:
        'references/used/{group}/refmap/inpref.fa'
    shell:
        'ln -s $(pwd)/{input} {output}'

rule fasta_index:
    input:
        'references/used/{group}/refmap/{refori}.fa'
    output:
        'references/used/{group}/refmap/{refori}.fa.fai'
    conda:
        config['softparams']['conda']['minimap2']
    shell:
        'samtools faidx {input}'