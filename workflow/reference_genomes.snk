from pathlib import Path
from scripts.utilities import get_mapping_ref, get_ref, get_sim_param

rule download_ncbinuc:
    output:
        'references/download/{group}/ncbinuc_{ncbiid}.fasta'
    conda:
        config['softparams']['conda']['entrez']
    resources:
        ncbi_load=1
    shell:
        'efetch -db nucleotide -id "{wildcards.ncbiid}" -mode text -format fasta > {output} && [[ -s {output} ]]'

rule download_ncbiasbly:
    output:
        'references/download/{group}/ncbiasbly_{ncbiid}.fasta'
    conda:
        config['softparams']['conda']['entrez']
    resources:
        ncbi_load=1
    shell:
        'esearch -db assembly -query "{wildcards.ncbiid}" | elink -target nucleotide -name assembly_nuccore_refseq | efetch -format fasta > {output} && [[ -s {output} ]]'

rule simuG:
    input:
        exe = config['softparams']['soft']['simuG'],
        ref = lambda wc: get_sim_param(wc, config, 'ref')
    output:
        'references/simulated/{group}/{sample}.simseq.genome.fa'
    params:
        snp_count = lambda wc: get_sim_param(wc, config, 'snp_count'),
        indel_count = lambda wc: get_sim_param(wc, config, 'indel_count'), 
        seed = lambda wc: get_sim_param(wc, config, 'seed'),
        prefix = 'references/simulated/{group}/{sample}'
    log:
        'logs/{group}/simuG.{sample}.log'
    shell:
        '{input.exe} -r {input.ref} -p {params.prefix} -snp_count {params.snp_count} -indel_count {params.indel_count} -seed {params.seed} > {log}'

rule link_fasta:
    input:
        lambda wc: get_ref(wc, config)
    output:
        'references/used/{group}/sample/{sample}.fasta'
    run:
        if input[0].endswith('gz'):
            shell('zcat {input} > {output}')
        else :
            shell('ln -s $(pwd)/{input} {output}')

rule refmap:
    input:
        lambda wc: get_mapping_ref(wc, config)
    output:
        'references/used/{group}/refmap/inpref.fa'
    shell:
        'ln -s $(pwd)/{input} {output}'

rule fasta_index:
    input:
        'references/used/{group}/refmap/{refori}.fa'
    output:
        'references/used/{group}/refmap/{refori}.fa.fai'
    conda:
        config['softparams']['conda']['minimap2']
    shell:
        'samtools faidx {input}'